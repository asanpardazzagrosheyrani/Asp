# Build Your First DAO - Add Basic Functionalities

Hey folks, welcome back! You've done an awesome job making it this far in the course. Till now, you've learned about Q, DAOs in general and setting up the development environment.

 Now, we will dive into building the actual DAO. We will start coding in this lesson.

## DAO functionalities

We are building a simple DAO where users will be able to vote on proposal during the voting period created by the DAO. Once the voting period ends, the proposal is automatically executed on chain. In order to achieve that, we will define the following functionalities: 

1. **Add Members:** Our DAO will have the functionality to add members to the DAO. The added members will be the ones who will be able to vote on proposal. The creator of the DAO (who will deploy the DAO) will be the only one who has access to add members to the DAO.
2. **Remove Members:** Our DAO will also have a functionality of removing members from the DAO and only the creator of the DAO (who will deploy the DAO) can do it.
3. **Create Proposal:** This functionality will help DAOs to create a proposal and members of the DAO will be able to vote. Whether the proposal will be implemented or rejected will depend on the number of votes received in favor or against the proposal. 
4. **Vote:** Vote functionality will allow all of the members to vote on a particular proposal and amount of tokens they want to use for voting.
5. **Execute Proposal:** This will execute a proposal based on the votes made by the members of DAO. This function will have a mathematical expression to check if the majority of people in the DAO have voted for the proposal or not.

## Let’s start coding

So, we have discussed the high level functionalities you will add to your DAO. Now, you will code a DAO from scratch. Let’s start coding!

We’re going to start out with the basic structure every smart contract starts with.

```
// SPDX-License-Identifier: MIT
pragma solidity ^0.8.0;
```

Let’s look at what exactly we did here.

- First Line is comment that specifies the licensing right of the smart contract and this specific line gives the right to you and everyone to use this smart contract freely :).
- Second line is the version of the Solidity compiler that we want our contract to use. We’re basically telling the compiler to only use `0.8.0` or a higher version but not any lower version.

Now, we will initialise the DAO contract and add each functionality in this contract.

```
contract DAO {

	// Rest of the code goes here

}
```

Let’s start adding functionalities to this contract. 

### Struct proposal

```
struct Proposal {
		string description;
    uint voteCount;
    uint yesVotes;
    uint noVotes;
    bool executed;
 }
```

- First of all, we will add a `Proposal` struct to the contract that will represent a proposal within the DAO. **struct** in solidity  is a data structure that could have multiple data types like in our `Proposal` struct. The `Proposal` will be used to keep track of any kind of proposal presented by any member of the DAO.
- This struct has three properties:
    - `description`: A string describing the proposal and giving information about what the proposal is about.
    - `voteCount`: The total number of votes received by the proposal. This includes both the votes against and in favor of the proposal/
    - `executed`: A boolean indicating whether the proposal has been executed.
    - `yesVotes`: Total number of votes in favour of the proposal.
    - `noVotes:` Total number of votes against the proposal

### Struct member

```
struct Member {
		address memberAddress;
    uint memberSince;
    uint tokenBalance;
}
```

- This defines another struct called `Member` which has information about the member of the DAO.
- It has three properties:
    - `memberAddress`: The public wallet address of the member.
    - `memberSince`: The timestamp when the member joined the DAO.
    - `tokenBalance`: The number of tokens held by the member which could be used for voting.

### State variables

```

address[] public members;
mapping(address => bool) public isMember;
mapping(address => Member) public memberInfo;
mapping(address => mapping(uint => bool)) public votes; 
Proposal[] public proposals;
```

- Let’s define some state variables which will be used by the `DAO` contract.
    - `members`: This is an array of account addresses representing the members of the DAO.
    - `memberInfo`: This is a mapping that associates each member's account address with their corresponding `Member` struct. This would mean that each members’ info in struct will be associated with their address.
    - `votes`: The variable is a mapping that keeps track of whether a member has voted on a specific proposal. It is a nested mapping, a mapping within a mapping, where it checks the conditions whether the member has voted on a particular proposal (with a particular ID) and returns a true or false value accordingly.
    - `proposals`: The variable is an array of `Proposal` structs, representing the proposals made within the DAO. You can find all the proposals created by the DAO within this struct.
    - `isMember`: This checks if the address is a member or not and sets the boolean value accordingly.

### Token balances variables

```
uint public totalSupply;
mapping(address => uint) public balances;
```

- These variables are related to token balances.
    - `totalSupply`: The variable represents the total number of tokens in circulation.
    - `balances`: The variable is a mapping that associates each member's account address with their token balance.

### Events

```
event ProposalCreated(uint indexed proposalId, string description);
event VoteCast(address indexed voter, uint indexed proposalId, uint tokenAmount);
event ProposalAccepted(string message);
event ProposalRejected(string rejected);
```

- We will be using above Events in our contract.
- Events allow external systems to be notified when specific actions occur in the contract. Information from events could be utilized to display certain things on frontend depending on whether the event has taken place or not.

Now that we have defined the variables we will be needing in our contract, let’s get started to build the contract to allow users to vote on proposal. In web3, this is also called as decentralized governance wherein a smart contract can execute the decisions based on votes on proposal by community members.

## Constructor

Like mentioned earlier, we have certain functions which only the owner should be able to execute. For example, anyone shoudn’t be able to add and remove members in a DAO but only owner should. We will be using constructor to achieve that. 

In simple terms, a **constructor** is a special function which is initialized during the deployment of smart contract. For example, say you are deploying a smart contract which allows users to deposit Ether. While doing so, you need to make sure that no one other than you(owner) should be able to withdraw amount from the contract. Also, no one should be able to change the owner other than yourself if required. This is achieved with constructor function. Let’s see constructor in action. 

```
address public owner;

constructor(){
   owner = msg.sender;
}
```

Above, we have defined the variable owner which gets set to the address that has deployed the contract. Hence, address that deployed the contract becomes the owner of the contract. 

We will be using this constructor in our contract to set the owner so that only the owner should be able to add and remove members from the DAO. 

Now that we have the owner of the contract, let’s start writing functions by first creating addMember and removeMember function to add and remove member from our DAO respectively. 

### Add member

**addMember** function will allow the owner of the DAO to add a member to the DAO. Let’s look at the function below -

```
function addMember(address _member) public {
      require(msg.sender == owner);
      require(isMember[_member] == false, "Member already exists");
      memberInfo[_member] = Member({
          memberAddress: _member,
          memberSince: block.timestamp,
          tokenBalance: 100
      });
      members.push(_member);
      isMember[_member] = true;
      balances[_member] = 100;
      totalSupply += 100;
}
```

- The `addMember` function takes an account address `_member` as an argument and adds them as a member if they don't already exist.
- It sets the member’s information, adds them to the `members` array, assigns a token balance of 100 tokens to the member, and increases the total supply of all tokens available by 100 tokens.

### Remove member

**removeMember** function will allow the owner of the DAO to remove a member from the DAO. 

```
function removeMember(address _member) public {
    require(msg.sender == owner);
    require(isMember[_member] == true, "member does not exist");
    memberInfo[_member] = Member({
        memberAddress: address(0),
        memberSince: 0,
        tokenBalance: 0
    });
    for (uint i = 0; i < members.length; i++) {
        if (members[i] == _member) {
            members[i] = members[members.length - 1];
            members.pop();
            break;
        }
    }
    isMember[_member] = false;
    balances[_member] = 0;
    totalSupply -= 100;
}
```

- The `removeMember` function is used to remove a member from the DAO.
- It takes an account address `_member` as an argument and removes them as a member if they exist (if they are a member).
- If the member exists, the function proceeds to remove them from the DAO by updating the necessary data:
    - It sets the member's information in the `memberInfo` mapping to empty values. The member's address is set to the zero address (`address(0)`), the member's `memberSince` timestamp is set to 0, and the member's token balance is set to 0.
    - It iterates over the `members` array to find the index of the member being removed. Once found, it replaces the member's address with the address at the end of the array (`members[members.length - 1]`). Then, it removes the last element from the array using the `pop()` function. This ensures that the `members` array remains contiguous without leaving any gaps.
    - The function sets the member's token balance in the `balances` mapping to 0.
- After updating the data, the member has been effectively removed from the DAO.

## That’s a wrap

That’s pretty much it for now. We will continue our code in next lesson and add more taste to our recipe.
