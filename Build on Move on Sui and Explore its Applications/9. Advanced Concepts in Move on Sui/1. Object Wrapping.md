# Object Wrapping

I’m so proud of you for coming this far in the course. You have covered all of the basics so far. Not only this, you have also understood how the “Hello World” program works. Now it is time to go a little further and cover some advanced topics. In this lesson, we will cover the object wrapping that will help you immensely when you dive into building your dApps on Sui.

## Object wrapping

In the Sui Move programming language, you can organize data structures by putting one data structure inside another through something called **wrapping**. Imagine this to be something like a toolbox holding different items inside it. In Sui Move, you can organize data structures by putting a field of `struct` type in another, like below:

```
struct Foo has key {
    id: UID,
    bar: Bar,
}

struct Bar has key, store {
    id: UID,
    value: u64,
}
```

For a struct to be a Sui object, it should have the `key` ability. Now if you put a Sui object of type `Bar` into a Sui object of type `Foo`, the object type `Foo` wraps the object type `Bar`. The object type `Foo` is the wrapper or wrapping object. 

In the Sui Move code, you can put a Sui object as a field of a non-Sui object struct type. 

- Imagine you have a struct called "Foo" that is not a Sui object, and another struct called "Bar" that is a Sui object with the "key" and "store" abilities. You can put "Bar" inside "Foo," but this is temporary and won't persist on the blockchain.
- When you wrap an object, it becomes part of the data of the wrapping object. This means you can't look it up by its unique ID, and you can't pass the wrapped object as an argument in your blockchain transactions. The only way to access it is through the object that wraps it.
- You can't create a situation where, for example, object A wraps object B, B wraps C, and C also wraps A. This would lead to complexity and issues in your code.
- You can "unwrap" the wrapped object. When you unwrap it, it becomes an independent object again and can be accessed directly on the blockchain. The object retains the same unique ID it had before wrapping.

There are a few common ways to wrap a Sui object into another Sui object. Let’s look at them below:

### Direct wrapping

If you put a Sui object type directly as a field in another Sui object type, it is called **direct wrapping**. In this type of wrapping, the wrapped object cannot be unwrapped unless the wrapping object is destroyed. It could be used to lock objects with limited access.

Let’s see an example. Say you have a type of NFT-style object in Sui called `Object`, which has properties like `scarcity` and `style`. To ensure that there is a fair trade, you need to make sure that the scarcity is identical and only styles are different. This is because the value of NFTs is higher if it has more scarcity.  Here's how you can implement it:

```
struct Object has key, store {
    id: UID,
    scarcity: u8,
    style: u8,
}

public entry fun create_object(scarcity: u8, style: u8, ctx: &mut TxContext) {
    let object = Object {
        id: object::new(ctx),
        scarcity,
        style,
    };
    transfer::transfer(object, tx_context::sender(ctx));
}
```

In the above code, you can create an Object with specified scarcity and style using the `create_object` function. Anyone can call it, and the created object is sent to the transaction sender.

Now, you might recall from the previous sections that only object owners can send a transaction to mutate the object. So one person cannot send a transaction that would swap their own object with someone else's object. Hence, anyone who wants to swap their objects needs to send their objects to a third party like a swapping site. To ensure that you retain custody of your objects (such as coins and NFTs) and do not give full custody to the third party, you need to use direct wrapping like so:

```
struct ObjectWrapper has key {
    id: UID,
    original_owner: address,
    to_swap: Object,
    fee: Balance<SUI>,
}
```

The `ObjectWrapper` wraps the object to swap `to_swap` and tracks its original owner. You may also need to pay a fee for the swap.

To request a swap, you define an entry function:

```
public entry fun request_swap(object: Object, fee: Coin<SUI>, service_address: address, ctx: &mut TxContext) {
    assert!(coin::value(&fee) >= MIN_FEE, 0);
    let wrapper = ObjectWrapper {
        id: object::new(ctx),
        original_owner: tx_context::sender(ctx),
        to_swap: object,
        fee: coin::into_balance(fee),
    };
    transfer::transfer(wrapper, service_address);
}
```

In this function, the object to be swapped is wrapped using `ObjectWrapper`, and a fee is specified. The wrapper is then sent to a service address.

The crucial thing here is that even though the service operator owns the ObjectWrapper, they can't access or steal the wrapped "Object" inside it. This is because Sui doesn't allow access to the wrapped object without a specific unwrapping function, which is not defined in this module.

### Wrapping through `Option`

In the Direct wrapping example above you had to destroy an object to get the one inside it out. But sometimes, you might want more flexibility, where the wrapping type doesn't always have the wrapped object inside it, and you can replace the wrapped object with a different one when needed.

Let’s take an example of a warrior that has a sword and shield. He should have an option to either have or not have them. He should be able to add a new sword and shield or replace the current ones. 

In the code below, we define a `SimpleWarrior` type with optional `sword` and `shield` fields. The `sword` and `shield` are other Sui object types.

```
struct SimpleWarrior has key {
    id: UID,
    sword: Option<Sword>,
    shield: Option<Shield>,
}

struct Sword has key, store {
    id: UID,
    strength: u8,
}

struct Shield has key, store {
    id: UID,
    armor: u8,
}
```

When you create a new warrior, you start with no equipment:

```
public entry fun create_warrior(ctx: &mut TxContext) {
    let warrior = SimpleWarrior {
        id: object::new(ctx),
        sword: option::none(),
        shield: option::none(),
    };
    transfer::transfer(warrior, tx_context::sender(ctx))
}
```

This function `create_warrior` creates a new `SimpleWarrior` with no sword or shield.

To equip a new sword or shield, you can define functions like below:

```
public entry fun equip_sword(warrior: &mut SimpleWarrior, sword: Sword, ctx: &mut TxContext) {
    if (option::is_some(&warrior.sword)) {
        let old_sword = option::extract(&mut warrior.sword);
        transfer::transfer(old_sword, tx_context::sender(ctx));
    };
    option::fill(&mut warrior.sword, sword);
}
```

In the above function `equip_sword` you pass the warrior as a mutable reference and a sword to wrap into the warrior. If the warrior already has a sword, it first removes the old one and sends it back to the sender. Then, it wraps the new sword into the warrior. This way, the warrior can replace their equipment when they equip something new. 

## Wrapping through `vector`

In Sui, you can wrap objects in a vector field of another Sui object, which is similar to wrapping objects using the `Option` type. This approach allows an object to contain zero, one, or many wrapped objects of the same type.

Let’s consider an example involving pets and a farm:

```
struct Pet has key, store {
    id: UID,
    cuteness: u64,
}

struct Farm has key {
    id: UID,
    pets: vector<Pet>,
}
```

In this code, we have two object types:

1. `Pet`: Represents individual pets and has a unique ID and a "cuteness" value, which is just a placeholder for the pet's characteristics.
2. `Farm`: Represents a farm and has a unique ID as well. The key feature here is the `pets` field, which is a vector of `Pet` objects. This means a farm can contain multiple pets.

With this setup, you can add multiple pets to a farm. Each pet is wrapped in a `Pet` object, and these objects are collected and stored in the `pets` field of the farm object. This allows you to manage and access all the pets associated with a particular farm through the farm object.

## That’s a wrap

In this lesson, you got fully equipped with the object wrapping. You know very well what it is and how you can work with it in Move on Sui. In next lesson, we will move forward with more of advanced topics and learn events in Move on Sui.