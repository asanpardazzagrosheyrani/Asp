# Adding Image and Info to NFT

Welcome back! So by now you have written your NFT contract. In this lesson, we will add image and required information to NFT. So why wait, let’s continue!

## NFT metadata

Every NFT has some data linked to it in the form of JSON format that describes the NFT. We call it metadata. This metadata has a special JSON format and the format needs to be followed if we want our NFT to appear properly on platforms like OpenSea, Rarible, etc.

In the previous lesson, I shared how we wrote `zodiacSign` instead of sharing the Uniform Resource Identifier, I mean, the data about the NFT. 

Let’s look back at it.

```
_setTokenURI(newItemId, zodiacSign);
```

### Understanding metadata structure

Now we will use the standard format required by NFT platforms to describe the data related to the NFT. The following is the JSON structure, we are mentioning the name of the Image, Description, and attributes related to the Image. With Image, you can give a link to the externally hosted image, or just create an SVG structure.

```
{
    "name":"Virgo",
    "description":"Perfectionist. Great Educators. Overthinkers.",
    "image":"your-hosted-img-url.jpg or svg",
    "attributes":
    [
        { "trait_type":"Zodiac","value":"Virgo"
        },
        { "trait_type":"Month","value":"August"
        }
    ]
}
```

For this project, I am creating a basic black box of SVG, that has the zodiac name mentioned in the center. I’d urge you to be creative and come up with something exciting.

```
<svg width="350" height="350" xmlns="<http://www.w3.org/2000/svg>" preserveAspectRatio="xMinYMin meet">
 <g>
  <title>Layer 1</title>
  <rect id="svg_1" fill="black" height="100%" width="100%"/>
  <text id="svg_2" font-size="24px" font-family="serif" fill="white" text-anchor="middle" dominant-baseline="middle" class="base" y="50%" x="50%">Virgo</text>
 </g>
</svg>
```

This is my SVG structure, I will dynamically replace Virgo with the calculated Zodiac sign in the future. 

Now lets finalize our metadata and then run this smart contract.

## Update `horoscopeNFT.sol`

First of all, I will go back to my `horoscopeNFT` contract file and add the following imports to the `horoscopeNFT.sol` file.

```
import "@openzeppelin/contracts/token/ERC721/extensions/ERC721URIStorage.sol";
import "@openzeppelin/contracts/utils/Base64.sol";
```

Then, I will make my SVG code dynamic so that the zodiac sign name can be added to the image in run time. I will declare string type `baseSvg` and add the first part of my SVG. **The following line will go right after the counter declaration: `uint256 private _tokenIds;`**

```
string baseSvg = "<svg xmlns='<http://www.w3.org/2000/svg>' preserveAspectRatio='xMinYMin meet' viewBox='0 0 350 350'><style>.base { fill: white; font-family: serif; font-size: 24px; }</style><rect width='100%' height='100%' fill='black' /><text x='50%' y='50%' class='base' dominant-baseline='middle' text-anchor='middle'>";

```

Next, I will open the `mintNFT` function and replace the existing code with the following code.

```
function mintNFT(address recipient, string memory zodiacSign)
   public
   returns (uint256)
{
   _tokenIds.increment();

   string memory finalSvg = string(
       abi.encodePacked(baseSvg, zodiacSign, "</text></svg>")
   );

   // Get all the JSON metadata in place and base64 encode it.
   string memory json = Base64.encode(
       bytes(
           string(
               abi.encodePacked(
                   '{"name": "',zodiacSign,
                   // We set the title of our NFT as the generated word.
                   '", "description": "On-chain Zodiac Sign NFTs", "attributes": [{"trait_type": "Zodiac Sign", "value": "',
                   zodiacSign,
                   '"}], "image": "data:image/svg+xml;base64,',
                   // We add data:image/svg+xml;base64 and then append our base64 encode our svg.
                   Base64.encode(bytes(finalSvg)),
                   '"}'
               )
           )
       )
   );

   // Just like before, we prepend data:application/json;base64, to our data.
   string memory finalTokenUri = string(
       abi.encodePacked("data:application/json;base64,", json)
   );

   uint256 newItemId = _tokenIds.current();
   _mint(recipient, newItemId);
   _setTokenURI(newItemId, finalTokenUri);

   return newItemId;
}
```

Let me explain the code step by step.

```
 string memory finalSvg = string(
        abi.encodePacked(baseSvg, zodiacSign, "</text></svg>")
    );
```

- Here we are just concatenating the `baseSvg`, the `zodiacSign`, and the end tags. Then we wrap it in string just to make sure that our final outcome is string type.

```
// Get all the JSON metadata in place and base64 encode it.
    string memory json = Base64.encode(
        bytes(
            string(
                abi.encodePacked(
                    '{"name": "',zodiacSign,
                    // We set the title of our NFT as the generated word.
                    '", "description": "On-chain Zodiac Sign NFTs", "attributes": [{"trait_type": "Zodiac Sign", "value": "',
                    zodiacSign,
                    '"}], "image": "data:image/svg+xml;base64,',
                    // We add data:image/svg+xml;base64 and then append our base64 encode our svg.
                    Base64.encode(bytes(finalSvg)),
                    '"}'
                )
            )
        )
    );
```

- We create our JSON structure and encode it in `base64`. Similarly, we populate `finalTokenURI` with JSON structure, encode it in `base64`, and declare the format to be JSON.

```
// Just like before, we prepend data:application/json;base64, to our data.
    string memory finalTokenUri = string(
        abi.encodePacked("data:application/json;base64,", json)
    );

```

## Complete code

Awesome! Phew!! The hardest part is over. This is what the final contract looks like.

```
// SPDX-License-Identifier: UNLICENSED

pragma solidity ^0.8.20;

import "@openzeppelin/contracts/token/ERC721/extensions/ERC721URIStorage.sol";
import "hardhat/console.sol";
import "@openzeppelin/contracts/token/ERC721/extensions/ERC721URIStorage.sol";
import "@openzeppelin/contracts/utils/Base64.sol";

contract horoscopeNFT is ERC721URIStorage {
    uint256 private _tokenIds;

    string baseSvg = "<svg xmlns='<http://www.w3.org/2000/svg>' preserveAspectRatio='xMinYMin meet' viewBox='0 0 350 350'><style>.base { fill: white; font-family: serif; font-size: 24px; }</style><rect width='100%' height='100%' fill='black' /><text x='50%' y='50%' class='base' dominant-baseline='middle' text-anchor='middle'>";

    constructor() ERC721("Horoscope", "HSCP") {}

    function mintNFT(address recipient, string memory zodiacSign)
        public
        returns (uint256)
    {
        _tokenIds += 1;
        

        string memory finalSvg = string(
            abi.encodePacked(baseSvg, zodiacSign, "</text></svg>")
        );

        // Get all the JSON metadata in place and base64 encode it.
        string memory json = Base64.encode(
            bytes(
                string(
                    abi.encodePacked(
                        '{"name": "',zodiacSign,
                        // We set the title of our NFT as the generated word.
                        '", "description": "On-chain Zodiac Sign NFTs", "attributes": [{"trait_type": "Zodiac Sign", "value": "',
                        zodiacSign,
                        '"}], "image": "data:image/svg+xml;base64,',
                        // We add data:image/svg+xml;base64 and then append our base64 encode our svg.
                        Base64.encode(bytes(finalSvg)),
                        '"}'
                    )
                )
            )
        );

        // Just like before, we prepend data:application/json;base64, to our data.
        string memory finalTokenUri = string(
            abi.encodePacked("data:application/json;base64,", json)
        );

        uint256 newItemId = _tokenIds;
        _safeMint(recipient, newItemId);
        _setTokenURI(newItemId, finalTokenUri);
        return newItemId;
    }
}
```

Since we are encoding our JSON and SVG in Base64, we will need to import that file too from Openzeppelin using the `import "@openzeppelin/contracts/utils/Base64.sol";`. You can also download the file Base64 from [here](https://github.com/Metaschoolso/Learning-Projects/blob/main/Base64.sol) and add it to your contracts folder.

## That’s a wrap

Our function `mintNFT` is dynamic now, it takes the address and zodiac sign and then mints an NFT to the account.

Now, we will prepare for deployment.